defimpl Scrivener.Paginater, for: Ecto.Query do
  import Ecto.Query

  alias Scrivener.{Config, Page}

  @moduledoc false

  @spec paginate(Ecto.Query.t, Scrivener.Config.t) :: Scrivener.Page.t
  def paginate(query, %Config{page_size: page_size, page_number: page_number, module: repo, opts: opts}) do
    total_entries = total_entries(query, repo)

    %Page{
      page_size: page_size,
      page_number: page_number,
      entries: entries(query, repo, page_number, page_size, opts),
      total_entries: total_entries,
      total_pages: total_pages(total_entries, page_size)
    }
  end

  defp entries(query, repo, page_number, page_size, opts) do
    join_query_fn = Keyword.get(opts || [], :join_query_fn, fn q -> q end)

    offset = page_size * (page_number - 1)

    if joins?(query) do
      ids = query
      |> remove_clauses
      |> select([x], {x.id})
      |> group_by([x], x.id)
      |> join_query_fn.()
      |> offset([_], ^offset)
      |> limit([_], ^page_size)
      |> repo.all
      |> Enum.map(&elem(&1, 0))

      query
      |> where([x], x.id in ^ids)
      |> distinct(true)
      |> repo.all
    else
      query
      |> limit([_], ^page_size)
      |> offset([_], ^offset)
      |> repo.all
    end
  end

  defp total_entries(query, repo) do
    primary_key = query.from
    |> elem(1)
    |> apply(:__schema__, [:primary_key])
    |> hd

    query
    |> remove_clauses
    |> exclude(:order_by)
    |> select([m], count(field(m, ^primary_key), :distinct))
    |> repo.one!
  end

  defp joins?(query) do
    Enum.count(query.joins) > 0
  end

  defp remove_clauses(query) do
    query
    |> exclude(:preload)
    |> exclude(:select)
    |> exclude(:group_by)
  end

  defp total_pages(total_entries, page_size) do
    (total_entries / page_size) |> Float.ceil |> round
  end
end
